// Knowledge Base (47 lines):
matcher number "[0-9]*";
matcher sentence "[a-zA-Z][a-zA-Z ]*";
matcher word "[a-zA-Z]*";
matcher name "[A-Z][a-z]*";

matcher move {
  "moved";
  "journeyed";
  "went back";
  "went";
  "travelled";
}

matcher pick {
  "got";
  "grabbed";
  "took";
  "picked up";
}

matcher drop {
  "left";
  "dropped";
  "discarded";
  "put down";
}

matcher give {
  "gave";
  "handed";
  "passed";
}

matcher object {
  "apple";
  "football";
  "milk";
}

matcher place {
  "bathroom";
  "bedroom";
  "office";
  "hallway";
  "garden";
  "kitchen";
}

// Model (13 lines):
Person = map(
  '__init__': function() {
    this.where = "nowhere"
    this.items = map()
  }
)

choices = ["none", "one", "two", "three"]

var people = None
function reset() {
  people = map()
}

// Result Validation (9 lines):
var hit = 0
var miss = 0
function compare_results(guess, answer) {
  if (guess == answer) {
    hit = hit + 1;
  } else {
    miss = miss + 1
  }
}

// Parsing Complexity (54 lines):
matcher read {
  "(number); (name)n; (move); to the (place)p;." {
    if (people[n] == None) {
      people[n] = new Person();
    }

    people[n].where = p;
  }

  "(number); (name)n; (pick)v; the (object)obj;(".", " there.")w;" {
    if (people[n] == None) {
      people[n] = new Person();
    }

    // Move the object from the place to the person:
    people[n].items[obj] = True;
  }

  "(number); (name)n; (drop)v; the (object)o;(".", " there.")w;" {
    if (people[n] == None) {
      people[n] = new Person();
    }

    // Move the object from the person to the place:
    people[n].items.pop(o);
  }

  "(number); (name)n1; (give); the (object)obj; to (name)n2;." {
    if (people[n1] == None) {
      people[n1] = new Person();
    }

    if (people[n2] == None) {
      people[n2] = new Person();
    }

    people[n1].items.pop(obj);
    people[n2].items[obj] = True;
  }

  "(number); How many objects is (name)n; carrying? |(word)answer;|[0-9 ]*" {
    if (people[n] == None) {
      people[n] = new Person();
    }

    guess = choices[people[n].items.len()]
    compare_results(guess, answer);
  }
}

// Reading stories and executing the matcher network (32 lines):
nlines = 0
stories = list();

file = open('train7.txt')

// Collect data from all stories:
for (line in file.readlines()) {
  if (line[0] == '1' && line[1] == ' ') {
    story = list()
    stories.push(story)
  }
  story.push(line);
  nlines = nlines + 1
}

for (story in stories) {
  reset();
  for (line in story) {
    // print(line)
    if (read.match(line) == False) {
      print('missed this line: %s' % line)
    }
  }
}

print()
print('Done!');
print()
print('Total stories read: %s' % stories.len());
print('Total hits: %s' % hit);
print('Total miss: %s' % miss);
print()

