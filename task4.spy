
// Knowledge Base (19 lines):
matcher number "[0-9]*";
matcher word "[a-zA-Z]*";
matcher name "[A-Z][a-z]*";

matcher place {
  "bathroom";
  "bedroom";
  "office";
  "hallway";
  "garden";
  "kitchen";
}

matcher direction {
  "north" return {'text': 'north', 'opposed': 'south'};
  "south" return {'text': 'south', 'opposed': 'north'};
  "east" return {'text': 'east', 'opposed': 'west'};
  "west" return {'text': 'west', 'opposed': 'east'};
}

// Model (24 lines):
Place = map(
  'north': None,
  'south': None,
  'east': None,
  'west': None
)

function add_edge(p1, direction, p2) {
  places[p1][direction.text] = p2;
  places[p2][direction.opposed] = p1;
}

// Place list:
var places = None
var places_list = [
  "bathroom", "bedroom", "office",
  "hallway", "garden", "kitchen", "nowhere"
]
function reset() {
  places = map()
  for (name in places_list) {
    places[name] = new Place()
  }
}

// Result Validation (9 lines):
var hit = 0
var miss = 0
function compare_results(guess, answer) {
  if (guess == answer) {
    hit = hit + 1;
  } else {
    miss = miss + 1
  }
}

// Parsing Complexity (18 lines):
matcher read {
  "(number); The (place)p1; is (direction)d; of the (place)p2;." {
    var d = direction.match_one(d)
    add_edge(p2, d, p1);
  }

  "(number); What is the (place)p; (direction)d; of?|(word)answer;|[0-9 ]*" {
    var d = direction.match_one(d)
    guess = places[p][d.opposed]
    compare_results(guess, answer);
  }

  "(number); What is (direction)d; of the (place)p;?|(word)answer;|[0-9 ]*" {
    var d = direction.match_one(d)
    guess = places[p][d.text]
    compare_results(guess, answer);
  }
}

// Reading stories and executing the matcher network (30 lines):
stories = list();

file = open('train4.txt')

// Collect data from all stories:
for (line in file.readlines()) {
  if (line[0] == '1' && line[1] == ' ') {
    story = list()
    stories.push(story)
  }
  story.push(line);
}

for (story in stories) {
  reset();
  for (line in story) {
    // print(line)
    if (read.match(line) == False) {
      print('missed this line: %s' % line)
    }
  }
}

print()
print('Done!');
print()
print('Total stories read: %s' % stories.len());
print('Total hits: %s' % hit);
print('Total miss: %s' % miss);
print()

