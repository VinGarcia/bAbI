// Knowledge Base (23 lines):
matcher number "[0-9]*";
matcher word "[a-zA-Z]*";
matcher name "[A-Z][a-z]*";

matcher verb {
  "moved";
  "journeyed";
  "went back";
  "went";
  "travelled";
}

matcher place {
  "bathroom";
  "bedroom";
  "office";
  "hallway";
  "garden";
  "kitchen";
  "cinema";
  "park";
  "school";
}

// Model (23 line):
var where_is = map()

Person = map(
  '__init__': function(place1, place2) {
    if (place2) {
      this[place1] = 'maybe';
      this[place2] = 'maybe';
    } else {
      this[place1] = 'yes';
    }
  },

  // Used as default values for the class instances:
  'bathroom': 'no',
  'bedroom': 'no',
  'office': 'no',
  'hallway': 'no',
  'garden': 'no',
  'kitchen': 'no',
  'cinema': 'no',
  'park': 'no',
  'school': 'no'
)

// Result Validation (9 lines):
var hit = 0
var miss = 0
function compare_results(guess, answer) {
  if (guess == answer) {
    hit = hit + 1;
  } else {
    miss = miss + 1
  }
}

// Parsing Complexity (18 lines):
matcher read {
  "(number); (name)name; (verb); to the (place)place;." {
    where_is[name] = new Person(place, None);
  }

  "(number); (name)name; is in the (place)place;." {
    where_is[name] = new Person(place, None);
  }

  "(number); (name)name; is either in the (place)p1; or the (place)p2;." {
    where_is[name] = new Person(p1, p2);
  }

  "(number); Is (name)name; in the (place)place;? |(word)answer;|(number);" {
    guess = where_is[name][place]
    compare_results(guess, answer);
  }
}

// Reading stories and executing the matcher network (28 lines):
stories = list();

file = open('train10.txt')

// Collect data from all stories:
for (line in file.readlines()) {
  if (line[0] == '1' && line[1] == ' ') {
    story = list()
    stories.push(story)
  }
  story.push(line);
}

for (story in stories) {
  for (line in story) {
    if (read.match(line) == False) {
      print('Missed this line: %s!' % line)
    }
  }
}

print()
print('Done!');
print()
print('Total stories read: %s' % stories.len());
print('Total hits: %s' % hit);
print('Total miss: %s' % miss);
print()

