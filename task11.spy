
// Knowledge Base (33 lines):
matcher number "[0-9]*";
matcher word "[a-zA-Z]*";
matcher name "[A-Z][a-z]*";

matcher move {
  "moved";
  "journeyed";
  "went back";
  "went";
  "travelled";
}

matcher place {
  "bathroom";
  "bedroom";
  "office";
  "hallway";
  "garden";
  "kitchen";
}

matcher pronoun { "he"; "she"; }
matcher she { "Mary"; "Sandra"; }
matcher he { "Daniel"; "John"; }
pronoun["he"] = he;
pronoun["she"] = she;

matcher after {
  "Afterwards";
  "After that";
  "Following that";
  "Then";
}

// Model (8 lines):
// Recent memory:
memory = list()
where_is = map();

function reset() {
  memory = list()
  where_is = map()
}

// Result Validation (9 lines):
var hit = 0
var miss = 0
function compare_results(guess, answer) {
  if (guess == answer) {
    hit = hit + 1;
  } else {
    miss = miss + 1
  }
}

// Parsing Complexity (25 lines):
matcher read {
  "(number); (name)n; (move); to the (place)p;." {
    // Remember this person:
    memory.push(n);
    where_is[n] = p;
  }

  "(number); (after); (pronoun)pn; (move); to the (place)p;." {
    // Find the name of the last `he` or `she`:
    for(name in memory.reverse()) {
      // If the pronoun fits the name:
      if(pronoun[pn].match(name)) {
        break;
      }
    }

    // Parse it recursively in a more simple format:
    read.match('1 %s moved to the %s.' % (name, p));
  }

  "(number); Where is (name)n;? |(word)answer;|[0-9 ]*" {
    guess = where_is[n];
    compare_results(guess, answer);
  }
}

// Reading stories and executing the matcher network (31 lines):
stories = list();

file = open('train11.txt')

// Collect data from all stories:
for (line in file.readlines()) {
  if (line[0] == '1' && line[1] == ' ') {
    story = list()
    stories.push(story)
  }
  story.push(line);
}

// Parse and answer each story:
for (story in stories) {
  reset();
  for (line in story) {
    // print(line)
    if (read.match(line) == False) {
      print('missed this line: %s' % line)
    }
  }
}

print()
print('Done!');
print()
print('Total stories read: %s' % stories.len());
print('Total hits: %s' % hit);
print('Total miss: %s' % miss);
print()

