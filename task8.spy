// Knowledge Base (40 lines):
matcher number "[0-9]*";
matcher word "[a-zA-Z]*";
matcher name "[A-Z][a-z]*";

matcher move {
  "moved";
  "journeyed";
  "went back";
  "went";
  "travelled";
}

matcher pick {
  "got";
  "grabbed";
  "took";
  "picked up";
}

matcher drop {
  "left";
  "dropped";
  "discarded";
  "put down";
}

matcher object {
  "apple";
  "football";
  "milk";
}

matcher place {
  "bathroom";
  "bedroom";
  "office";
  "hallway";
  "garden";
  "kitchen";
}

// Model (29 lines):
Person = map(
  '__init__': function() {
    this.where = "nowhere"
    this.items = map()
  }
)

var people = None
function reset() {
  people = map()
}

// Not counting these lines, since the `set` container
// will be included in JSpy soon.
Set = map(
  '__init__': function(items) {
    this.items = {}
    if (items != None) {
      for (item in items) {
        this.items[str(item)] = True;
      }
    }
  },
  'equals': function(other_set) {
    // This is likely to stop working in
    // future versions of JSpy:
    return this == other_set;
  }
)

// Result Validation (13 lines):
var hit = 0
var miss = 0
function compare_results(guess, answer) {
  guess = new Set(guess)
  answer = new Set(answer)
  // Make sure `nothing` won't count as an item:
  answer.items.pop('nothing')
  if (guess.equals(answer)) {
    hit = hit + 1
  } else {
    miss = miss + 1
  }
}

// Parsing Complexity (36 lines):
matcher read {
  "(number); (name)n; (move); to the (place)p;." {
    if (people[n] == None) {
      people[n] = new Person();
    }

    people[n].where = p;
  }

  "(number); (name)n; (pick)v; the (object)obj;(".", " there.")w;" {
    if (people[n] == None) {
      people[n] = new Person();
    }

    // Move the object from the place to the person:
    people[n].items[obj] = True;
  }

  "(number); (name)n; (drop)v; the (object)o;(".", " there.")w;" {
    if (people[n] == None) {
      people[n] = new Person();
    }

    // Move the object from the person to the place:
    people[n].items.pop(o);
  }

  "(number); What is (name)n; carrying? |("[a-z,]*")answer;|[0-9 ]*" {
    if (people[n] == None) {
      people[n] = new Person();
    }

    guess = people[n].items
    compare_results(guess, answer.split(','));
  }
}

// Reading stories and executing the matcher network (32 lines):
nlines = 0
stories = list();

file = open('train8.txt')

// Collect data from all stories:
for (line in file.readlines()) {
  if (line[0] == '1' && line[1] == ' ') {
    story = list()
    stories.push(story)
  }
  story.push(line);
  nlines = nlines + 1
}

for (story in stories) {
  reset();
  for (line in story) {
    // print(line)
    if (read.match(line) == False) {
      print('missed this line: %s' % line)
    }
  }
}

print()
print('Done!');
print()
print('Total stories read: %s' % stories.len());
print('Total hits: %s' % hit);
print('Total miss: %s' % miss);
print()

