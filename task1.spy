
// Knowledge Base (20 lines):
matcher number "[0-9]*";
matcher word "[a-zA-Z]*";
matcher name "[A-Z][a-z]*";

matcher verb {
  "moved";
  "journeyed";
  "went back";
  "went";
  "travelled";
}

matcher place {
  "bathroom";
  "bedroom";
  "office";
  "hallway";
  "garden";
  "kitchen";
}

// Model (1 line):
where_is = map();

// Result Validation (9 lines):
hit=0
miss=0
function compare_results(guess, answer) {
  if (guess == answer) {
    hit = hit + 1;
  } else {
    miss = miss + 1
  }
}

// Parsing Complexity (12 lines):
matcher read {
  "(number); (name)name; (verb); to the (place)place;." {
    where_is[name] = place;
    finish True
  }

  "(number); Where is (name)name;? |(word)answer;|(number);" {
    guess = where_is[name]
    compare_results(guess, answer);
    finish True
  }
}

// Reading stories and executing the matcher network (28 lines):
stories = list();

file = open('train1.txt')

// Collect data from all stories:
for (line in file.readlines()) {
  if (line[0] == '1' && line[1] == ' ') {
    story = list()
    stories.push(story)
  }
  story.push(line);
}

for (story in stories) {
  for (line in story) {
    if (read.exec(line).len() == 0) {
      print('missed one!')
    }
  }
}

print()
print('Done!');
print()
print('Total stories read: %s' % stories.len());
print('Total hits: %s' % hit);
print('Total miss: %s' % miss);
print()

