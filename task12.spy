// Knowledge Base (20 lines):
matcher number "[0-9]*";
matcher word "[a-zA-Z]*";
matcher name "[A-Z][a-z]*";

matcher move {
  "moved";
  "journeyed";
  "went back";
  "went";
  "travelled";
}

matcher place {
  "bathroom";
  "bedroom";
  "office";
  "hallway";
  "garden";
  "kitchen";
}

// Model (1 line):
where_is = map();

// Result Validation (9 lines):
var hit = 0
var miss = 0
function compare_results(guess, answer) {
  if (guess == answer) {
    hit = hit + 1;
  } else {
    miss = miss + 1
  }
}

// Parsing Complexity (16 lines):
matcher read {
  "(number); (name)n; (move); to the (place)p;." {
    // print(text)
    where_is[n] = p;
  }

  "(number); (name)n1; and (name)n2; (move); to the (place)p;." {
    // Run it recursively:
    read.match('1 %s went to the %s.' % (n1, p));
    read.match('1 %s went to the %s.' % (n2, p));
  }

  "(number); Where is (name)n;? |(word)answer;|(number);" {
    compare_results(where_is[n], answer)
  }
}

// Reading stories and executing the matcher network (31 lines):
nlines = 0
stories = list();

file = open('train12.txt')

// Collect data from all stories:
for (line in file.readlines()) {
  if (line[0] == '1' && line[1] == ' ') {
    story = list()
    stories.push(story)
  }
  story.push(line);
  nlines = nlines + 1
}

for (story in stories) {
  for (line in story) {
    "print(line)"
    if (read.match(line) == False) {
      print('missed this line: %s!' % line)
    }
  }
}

print()
print('Done!');
print()
print('Total stories read: %s' % stories.len());
print('Total hits: %s' % hit);
print('Total miss: %s' % miss);
print()

